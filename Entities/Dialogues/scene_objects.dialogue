import "res://Entities/Dialogues/items.dialogue" as items

~ coat_hanger
if GameManager.get_dialogue_state("car_opened"):
	% I've already opened the car. I don't need to do that again.
	% As much as I enjoyed that, the clock is ticking.
	=> items/room_translator
do GameManager.set_dialogue_state("car_opened")
do PersistenceSystem.car_opened.emit()
do play_sound("jimmy_door")
You jam the coat hanger into car door, trying to find the lock
	After some fiddling, you get it to pop open.
	
=> items/room_translator

~ oven
It's a gas stove
	Maybe we can make a last meal.
=> items/room_translator

~ oven_candles
do InventorySystem.add_item(CANDLES_LIT)
do InventorySystem.remove_item(CANDLES)
do play_sound("light_candles")
You light the stove and hold the candles over the flames. They light!
=> items/room_translator

~ unhandled_item
%2 I can't unlock the car with that
% I need something really thin to jam in between the window and door
% That won't do
=> items/room_translator

~ car_opened
% I've already opened the car. I don't want to do that again
% As much as I enjoyed that, the clock is ticking
=> items/room_translator


~ car
if GameManager.get_dialogue_state("car_opened") and not GameManager.get_dialogue_state("knows_about_keys"):
	You opened the car without any help from anyone. Gold star!
	=> items/room_translator
if GameManager.get_dialogue_state("car_opened") and not GameManager.get_dialogue_state("knows_about_keys"):
	=> car_opened
if GameManager.get_dialogue_state("knows_about_keys") and GameManager.get_dialogue_state("reached_for_keys"):
	do GameManager.set_dialogue_state("car_ask_mark")
	David has the keys in his pocket, but there's no way I'm getting close to them.
	There must be some way to break in without forever wrecking the car.
	Maybe Mark knows?
	=> items/room_translator
if GameManager.get_dialogue_state("knows_about_keys") and not GameManager.get_dialogue_state("reached_for_keys"):
	David has the keys in his pocket. Maybe if I ask nicely he'll give them to me?
	=> items/room_translator
if not GameManager.get_dialogue_state("knows_about_keys"):
	You pull the handle on the door, but the door doesn't open.
		Locked! Maybe Lisa or Mark have the keys.
	=> items/room_translator

~ safe_opened
I aleady opened the safe and got the tape out. I don't need to do it again
=> items/room_translator

~ collected_tape
Hey, a tape! It looks old and says "Ritual" in brown-ish ... ink? Yeah. That's ink.
	Cassette Tape added to inventory
=> items/room_translator

~ dante
A copy of Dante's Inferno
	I'm not picking that up. I have my own 9th level of Hell right here in this cabin.
=> items/room_translator

~ pillow
It's a nice pillow on the bed
	Now is not the time to take a nap. We're going to die in minutes.
=> items/room_translator

~ knife_pillow
do PersistenceSystem.found_kazoo.emit()
You cut the pillow with a knife because why not? 
	Is that a kazoo?
=> items/room_translator